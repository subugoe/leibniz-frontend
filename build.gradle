// configuration according to https://objectpartners.com/2015/04/29/using-gradle-and-bower-to-manage-jscss-dependencies/

plugins {

  id "com.moowork.node" version "0.12"

}


node {

  version = "5.9.1"
  npmVersion = "3.7.3"
  download = true  

}


task npmCacheConfig(type: NpmTask) {

  description = "Configure the NPM cache"
  def npmCacheDir = "${gradle.getGradleUserHomeDir()}/caches/npm"
  outputs.files file(npmCacheDir)
  args = [ "config", "set", "cache", npmCacheDir ]

}


task npmPackages(type: NpmTask, dependsOn: npmCacheConfig) {

  description = "Install Node.js packages"
  args = [ "install" ]
  inputs.files file("package.json")
  outputs.files file("node_modules")

}


task bowerInstall(type: NodeTask) {

  description = "Configure bower install"
  script = file("node_modules/bower/bin/bower")
  args = [ "--config.storage.cache=${gradle.getGradleUserHomeDir()}/caches/bower/cache",
           "--config.storage.packages=${gradle.getGradleUserHomeDir()}/caches/bower/packages",
           "--config.storage.registry=${gradle.getGradleUserHomeDir()}/caches/bower/registry",
           "install" ]
  inputs.files file("bower.json")
  outputs.files file("bower_components")
  dependsOn npmPackages

}


task emberServe(type: Exec) {
  
  description = "Creating ember server"
  dependsOn node, npmPackages, bowerInstall

  commandLine "node_modules/ember-cli/bin/ember", "serve"

}


task emberBuildDev(type: Exec) {

  description = "Build development server"
  dependsOn node, npmPackages, bowerInstall

  commandLine "node_modules/ember-cli/bin/ember", "build", "--", "environment", "development"

}


task emberBuildProd(type: Exec) {

  description = "Build production server"
  dependsOn node, npmPackages, bowerInstall

  commandLine "node_modules/ember-cli/bin/ember", "build", "--", "environment", "production"

}



